// Copyright (c) 2018-2020, NVIDIA CORPORATION.  All rights reserved.
//
// NVIDIA CORPORATION and its licensors retain all intellectual property
// and proprietary rights in and to this software, related documentation
// and any modifications thereto.  Any use, reproduction, disclosure or
// distribution of this software and related documentation without an express
// license agreement from NVIDIA CORPORATION is strictly prohibited.

mdl 1.6;

import ::anno::*;
import ::base::*;
import ::limits::*;
import ::state::*;
import ::tex::*;
import ::math::*;

import ::OmniSurface::OmniSurfaceBase::*;
import ::OmniSurface::OmniShared::*;

export enum RLBlendMode
[[ 
    anno::description("Blend map blend mode")
]]
{
    blendmode_multiply = 0 [[ 
        anno::display_name("multiply") 
    ]],

    blendmode_add = 1 [[ 
        anno::display_name("add") 
    ]],

    blendmode_overlay = 2 [[ 
        anno::display_name("overlay") 
    ]]
};

float3 BlendMapByMode( float3 blend_color, float3 base_color, float weight, RLBlendMode mode )
{
    float3 top = blend_color;
    float3 bottom = base_color;

    float3 op_result = top;
        
    switch (mode) {
    case blendmode_multiply:
        op_result *= bottom;
        break;
    case blendmode_add:
        op_result += bottom;
        break;

    case blendmode_overlay:
        {

            float3 mul = base_color*blend_color;
            float3 add = base_color+blend_color;
            if (base_color.x >= .5) mul.x = add.x-mul.x-0.5;
            if (base_color.y >= .5) mul.y = add.y-mul.y-0.5;
            if (base_color.z >= .5) mul.z = add.z-mul.z-0.5;
            op_result = mul+mul;
        }
        break;
    }
    return math::lerp( bottom, op_result, weight );
}

// [ Sx   0   0 ]   [  cosR   sinR   0 ]   [  1   0   Tx ]   [  SxcosR   SxsinR  Tx ]
// [  0  Sy   0 ] * [ -sinR   cosR   0 ] * [  0   1   Ty ] = [ -SysinR   SycosR  Ty ]
// [  0   0   1 ]   [     0      0   1 ]   [  0   0    1 ]   [       0        0   1 ]
// CRenderKit::SetTextureMatrix
float3x3 GetUvFloat3x3( float2 kScale, float2 kTrans, float fRotate )
{
   	float fCos = math::cos( fRotate );
    float fSin = math::sin( fRotate );
    return float3x3 (
		 kScale[0] * fCos, kScale[0] * fSin, kTrans[0],
		-kScale[1] * fSin, kScale[1] * fCos, kTrans[1],
		 0,                0,                1 );
}

float2 TransformUV( float2 uv, float3x3 uvMatrix )
{
	float3 uvw = float3( uv[0], 1 - uv[1], 1 ) * uvMatrix;
	return float2( uvw[0], 1 - uvw[1] );
}


// equalivalent to OmniShared::add_detail_normal
float3 BlendAngleCorrectedNormals( float3 BaseNormal, float3 SubNormal )
{
    float ZPlus1 = BaseNormal.z + 1;
    float3 vBaseZPlus1 = float3( BaseNormal.x, BaseNormal.y, ZPlus1 );
    float3 vSubFlipRG = float3( -SubNormal.x, -SubNormal.y, SubNormal.z );
    float fDot = math::dot( vBaseZPlus1, vSubFlipRG );
    return math::normalize( ( vBaseZPlus1 * fDot ) - ( ZPlus1 * vSubFlipRG ) );
}

float3 IcBlendOverlay( float3 a, float3 b )
{
	float3 vA = 2 * a * b;
	float3 vB = float3(1,1,1) - 2*(float3(1,1,1)-a)*(float3(1,1,1)-b);
	float f0 = ( a.x < 0.5 ) ? vA.x : vB.x;
	float f1 = ( a.y < 0.5 ) ? vA.y : vB.y;
	float f2 = ( a.z < 0.5 ) ? vA.z : vB.z;
	return float3( f0, f1, f2 );
}

float CheapContrast( float In, float Contrast )
{
    return math::saturate( math::lerp( 0 - Contrast, Contrast + 1, In ) );
}

float HQ_HeadRoughnessArea(
	float Neck,
	float Ear,
	
	float Nose,
	float Mouth,
	float Upperlid,
	float Innerlid,
	
	float Cheek,
	float Forehead,
	float Upperlip,
	float Chin,
	
	float2 NeckEar,
	float4 NoseMouthUpperInnerLid,
	float4 CheekForeheadUpperLipChin,
	float UnmaskScale
	)
{
    float2 t1 = NeckEar;
    float neck = Neck* t1.x;
    float ear  = Ear * t1.y;

    float4 t2 = NoseMouthUpperInnerLid;
    float nose     = Nose     * t2.x;
    float mouth    = Mouth    * t2.y;
    float upperlid = Upperlid * t2.z;
    float innerlid = Innerlid * t2.w;

    float4 t3 = CheekForeheadUpperLipChin;
    float check    = Cheek    * t3.x;
    float forehead = Forehead * t3.y;
    float upperlip = Upperlip * t3.z;
    float chin     = Chin     * t3.w;

    float maskSum = math::saturate( t1.x + t1.y + t2.x + t2.y + t2.z + t2.w + t3.x + t3.y + t3.z + t3.w );

    float sum = ear + neck + nose + mouth + upperlid + innerlid + check + forehead + upperlip + chin
                + ( 1 - maskSum ) * UnmaskScale;
    return sum;
}

float HQ_HeadSSSArea(
	float Neck,
	float Ear,
	
	float Nose,
	float Mouth,
	float Upperlid,
	float Innerlid,
	
	float Cheek,
	float Forehead,
	float Upperlip,
	float Chin,
	
	float2 NeckEar,
	float4 NoseMouthUpperInnerLid,
	float4 CheekForeheadUpperLipChin,
	float UnmaskScale,
	
	float SSSMap
	)
{
    float2 t1 = NeckEar;
    float neck = (Neck-1)* t1.x;
    float ear  = (Ear-1) * t1.y;

    float4 t2 = NoseMouthUpperInnerLid;
    float nose     = (Nose-1)     * t2.x;
    float mouth    = (Mouth-1)    * t2.y;
    float upperlid = (Upperlid-1) * t2.z;
    float innerlid = (Innerlid-1) * t2.w;

    float4 t3 = CheekForeheadUpperLipChin;
    float cheek    = (Cheek-1)    * t3.x;
    float forehead = (Forehead-1) * t3.y;
    float upperlip = (Upperlip-1) * t3.z;
    float chin     = (Chin-1)     * t3.w;

    float maskSum = math::saturate( t1.x + t1.y + t2.x + t2.y + t2.z + t2.w + t3.x + t3.y + t3.z + t3.w );

    float sum = ear + neck + nose + mouth + upperlid + innerlid + cheek + forehead + upperlip + chin
                + ( 1 - maskSum ) * (UnmaskScale-1);
    return math::lerp( 0.08, 1, math::saturate(sum + SSSMap) );
}

export material RLHead(

	///////////////////////////////////////////////////
    //Diffuse reflection
    //
    color diffuse_color_constant = color(1.0f)
    [[
        anno::display_name("Base Color"),
        anno::description("Material Base Color"),
        anno::in_group("Base")
    ]],    
    float diffuse_reflection_weight = float(1.0) [[
        anno::display_name("Weight"),
        anno::in_group("Base"),
        anno::hard_range(0.0f, 1.0f)
    ]],

    uniform texture_2d diffuse_reflection_color_image = texture_2d() [[ 
        anno::display_name("Color Image"),
        anno::in_group("Base")
    ]],

    uniform texture_2d blend_map = texture_2d()
    [[
        anno::display_name("Blend Map"),
        anno::in_group("Base")
    ]],
    uniform float blend_map_strength = 1.0f
    [[
        anno::display_name("Blend Map Strength"),
        anno::in_group("Base"),
        anno::hard_range(0.0, 1.0)
    ]],
    uniform RLBlendMode blend_map_mode = blendmode_multiply
    [[
		anno::display_name("Blend Mode"),
        anno::in_group("Base")
    ]],
    
    uniform texture_2d BaseColor_Blend2_Image = texture_2d() [[ 
        anno::display_name("2nd Color Blend Image"),
        anno::in_group("Base")
    ]],
    uniform float BaseColor_Blend2_Strength = float(0.0) [[
        anno::display_name("BaseColor_Blend2_Strength"),
        anno::in_group("Base"),
        anno::hard_range(0.0f, 1.f)
    ]],
    
    ///////////////////////////////////////////////////
    // Normal
    uniform texture_2d geometry_normal_image = texture_2d() [[ 
        anno::display_name("Normal Map Image"),
        anno::in_group("Normal")
    ]],

    uniform bool geometry_normal_image_flip_g_channel = false [[
        anno::display_name("Normal Map Flip G Channel"),
        anno::in_group("Normal")
    ]],
    
    uniform float geometry_normal_strength = float(0.7f) [[
        anno::display_name("Normal Map Strength"),
        anno::in_group("Normal"),
        anno::soft_range(0.0f, 1.0f)
    ]],

    uniform texture_2d NormalMap_Blend_Image = texture_2d() [[ 
        anno::display_name("2nd Normal Map Image"),
        anno::in_group("Normal")
    ]],
    uniform float NormalMap_Blend_Strength = float(0.0) [[
        anno::display_name("NormalMap_Blend_Strength"),
        anno::in_group("Normal"),
        anno::hard_range(0.0f, 1.0f)
    ]],

    //Micro Normal
    //
    uniform texture_2d micro_normal_image = texture_2d() [[ 
        anno::display_name("Micro Normal Map Image"),
        anno::in_group("Micro Normal")
    ]],

    uniform texture_2d micro_normal_mask_image = texture_2d() [[ 
        anno::display_name("Micro Normal Mask Map Image"),
        anno::in_group("Micro Normal")
    ]],
    uniform bool Flip_MicroNormal_Y = false [[
        anno::display_name("Normal Map Image Flip G Channel"),
        anno::in_group("Micro Normal")
    ]],

    uniform float MicroNormal_Strength = float(0.5f) [[
        anno::display_name("MicroNormal_Strength"),
        anno::in_group("Micro Normal"),
        anno::soft_range(0.0f, 1.f)
    ]],
    uniform float MicroNormal_Tiling = float(25.0f) [[
        anno::display_name("MicroNormal_Tiling"),
        anno::in_group("Micro Normal"),
        anno::soft_range(0.0f, 20.0f)
   ]],
    
	///////////////////////////////////////////////////
    // RL Mask of Roughness and Subsurface
    uniform texture_2d Nose_Mouth_UpperInnerLid_Mask_Image = texture_2d() [[ 
        anno::display_name("Nose_Mouth_UpperInnerLid_Mask Image"),
        anno::in_group("Mask of Roughness and Subsurface")
    ]],
    uniform texture_2d Ear_Neck_Mask_Image = texture_2d() [[ 
        anno::display_name("Ear_Neck_Mask Image"),
        anno::in_group("Mask of Roughness and Subsurface")
    ]],
    uniform texture_2d Cheek_Fore_UpperLip_Chin_Mask_Image = texture_2d() [[ 
        anno::display_name("Cheek_Fore_UpperLip_Chin_Mask Image"),
        anno::in_group("Mask of Roughness and Subsurface")
    ]],
	///////////////////////////////////////////////////
	// Roughness
    uniform texture_2d specular_reflection_roughness_image = texture_2d() [[ 
        anno::display_name("Roughness Image"),
        anno::in_group("Roughness")
    ]],
    
    uniform float roughness_multiplier = float(1.0) [[
        anno::display_name("roughness multiplier"),
        anno::in_group("Roughness"),
        anno::soft_range(0.1f, 2.f)
    ]],
	
	    
    uniform float roughness_01 = float(0.75f) [[
        anno::display_name("roughness_min"),
        anno::in_group("Roughness"),
        anno::hard_range(0.1f, 2.f)
    ]], //Jeff 新增
	
	uniform float roughness_02 = float(1.35f) [[
        anno::display_name("roughness_max"),
        anno::in_group("Roughness"),
        anno::hard_range(0.1f, 2.f)
    ]],//Jeff 新增
	uniform float roughness_lerp = float(0.85f) [[
        anno::display_name("roughness_lerp"),
        anno::in_group("Roughness"),
        anno::hard_range(0.0f, 1.f)
    ]],//Jeff 新增

	
	
    
    //Roughness Advanced
    //
    uniform float Micro_Roughness_Scale = float(0.0) [[
        anno::display_name("Micro_Roughness_Scale"),
        anno::in_group("Roughness Advanced"),
        anno::soft_range(-1.5f, 1.5f)
    ]],
    uniform float Nose_Roughness_Scale = float(0.0) [[
        anno::display_name("Nose_Roughness_Scale"),
        anno::in_group("Roughness Advanced"),
        anno::hard_range(-1.5f, 1.5f)
    ]],
    uniform float Mouth_Roughness_Scale = float(0.0) [[
        anno::display_name("Mouth_Roughness_Scale"),
        anno::in_group("Roughness Advanced"),
        anno::hard_range(-1.5f, 1.5f)
    ]],
    uniform float UpperLid_Roughness_Scale = float(0.0) [[
        anno::display_name("UpperLid_Roughness_Scale"),
        anno::in_group("Roughness Advanced"),
        anno::hard_range(-1.5f, 1.5f)
    ]],
    uniform float InnerLid_Roughness_Scale = float(0.0) [[
        anno::display_name("InnerLid_Roughness_Scale"),
        anno::in_group("Roughness Advanced"),
        anno::hard_range(-1.5f, 1.5f)
    ]],
    uniform float Ear_Roughness_Scale = float(0.0) [[
        anno::display_name("Ear_Roughness_Scale"),
        anno::in_group("Roughness Advanced"),
        anno::hard_range(-1.5f, 1.5f)
    ]],
    uniform float Neck_Roughness_Scale = float(0.0) [[
        anno::display_name("Neck_Roughness_Scale"),
        anno::in_group("Roughness Advanced"),
        anno::hard_range(-1.5f, 1.5f)
    ]],
    uniform float Cheek_Roughness_Scale = float(0.0) [[
        anno::display_name("Cheek_Roughness_Scale"),
        anno::in_group("Roughness Advanced"),
        anno::hard_range(-1.5f, 1.5f)
    ]],
    uniform float Forehead_Roughness_Scale = float(0.0) [[
        anno::display_name("Forehead_Roughness_Scale"),
        anno::in_group("Roughness Advanced"),
        anno::hard_range(-1.5f, 1.5f)
    ]],
    uniform float UpperLip_Roughness_Scale = float(0.0) [[
        anno::display_name("UpperLip_Roughness_Scale"),
        anno::in_group("Roughness Advanced"),
        anno::hard_range(-1.5f, 1.5f)
    ]],
    uniform float Chin_Roughness_Scale = float(0.0) [[
        anno::display_name("Chin_Roughness_Scale"),
        anno::in_group("Roughness Advanced"),
        anno::hard_range(-1.5f, 1.5f)
    ]],
    uniform float Unmasked_Roughness_Scale = float(0.0) [[
        anno::display_name("Unmasked_Roughness_Scale"),
        anno::in_group("Roughness Advanced"),
        anno::hard_range(-1.5f, 1.5f)
    ]],

	///////////////////////////////////////////////////
    //Metal
    //
    uniform texture_2d metalness_image = texture_2d() [[ 
        anno::display_name("Metalness Image"),
        anno::in_group("Metallic")
    ]],
	///////////////////////////////////////////////////
    //Specular reflection
    //
    float specular_reflection_weight = float(0.4f) [[ 
        anno::display_name("Weight"),
        anno::in_group("Specular"),
        anno::soft_range(0.0f, 1.0f)
    ]],

    uniform texture_2d specular_reflection_weight_image = texture_2d() [[ 
        anno::display_name("Weight Image"),
        anno::in_group("Specular")
    ]],

    color specular_reflection_color = color(1.0) [[
        anno::display_name("Specular Color"),
        anno::in_group("Specular")
    ]],

    uniform ::OmniSurface::OmniShared::ior_presets specular_reflection_ior_preset = 
        ::OmniSurface::OmniShared::ior_custom [[
        anno::display_name("IOR Preset"),
        anno::in_group("Specular")
    ]],

    uniform float specular_reflection_ior = float(1.35f) [[ 
        anno::display_name("IOR"),    
        anno::in_group("Specular"),
        anno::soft_range(1.0f, 5.0f)
	]],
	
	///////////////////////////////////////////////////
	//Specular transmission
    //
    uniform bool enable_specular_transmission = false [[
        anno::display_name("Enable Transmission"),
        anno::description("Enables the use of transmission"),
        anno::in_group("Transmission")
    ]],

    float specular_transmission_weight = float(0.1) [[ 
        anno::display_name("Weight"),
        anno::in_group("Transmission"),
		anno::soft_range(0.0f, 1.0f),
        anno::hard_range(0.0f, 1.0f)
    ]],

    uniform texture_2d specular_transmission_weight_image = texture_2d() [[ 
        anno::display_name("Weight Image"),
        anno::in_group("Transmission")
    ]],

    color specular_transmission_color = color(1.0) [[
        anno::display_name("Color"),
        anno::in_group("Transmission")
    ]],

    uniform texture_2d specular_transmission_color_image = texture_2d() [[ 
        anno::display_name("Color Image"),
        anno::in_group("Transmission")
    ]],

    float specular_transmission_scattering_depth = float(1.0) [[ 
        anno::display_name("Depth"),
        anno::in_group("Transmission"),    
        anno::soft_range(0.0f, 100.0f),
        anno::hard_range(0.0f, limits::FLOAT_MAX)
    ]],

    uniform texture_2d specular_transmission_scattering_depth_image = texture_2d() [[ 
        anno::display_name("Depth Image"),
        anno::in_group("Transmission")
    ]],

    color specular_transmission_scattering_color = color(0.05f) [[ 
        anno::display_name("Scatter"),
        anno::in_group("Transmission")
    ]],

    uniform texture_2d specular_transmission_scattering_color_image = texture_2d() [[ 
        anno::display_name("Scatter Image"),
        anno::in_group("Transmission")
    ]],

    float specular_transmission_scatter_anisotropy = float(0.0) [[ 
        anno::display_name("Scatter Anisotropy"),
        anno::in_group("Transmission"),
		anno::soft_range(-1.0f, 1.0f),
        anno::hard_range(-1.0f, 1.0f)
    ]],

    uniform texture_2d specular_transmission_scatter_anisotropy_image = texture_2d() [[ 
        anno::display_name("Scatter Anisotropy Image"),
        anno::in_group("Transmission")
    ]],

    uniform float specular_transmission_dispersion_abbe = float(0.0) [[ 
        anno::display_name("Dispersion Abbe"),    
        anno::in_group("Transmission"),
        anno::soft_range(0.0f, 100.0f),
        anno::hard_range(0.0f, limits::FLOAT_MAX)
    ]],	
	
	///////////////////////////////////////////////////
    //Subsurface
    //
    uniform bool enable_diffuse_transmission = true [[
        anno::display_name("Enable Subsurface"),
        anno::description("Enables the use of subsurface"),
        anno::in_group("Subsurface")
    ]],

    uniform float subsurface_weight = float(0.65f) [[
        anno::display_name("Weight"),
        anno::in_group("Subsurface"),
        anno::soft_range(0.0f, 1.0f)
    ]],
    
    uniform texture_2d subsurface_weight_image = texture_2d() [[ 
        anno::display_name("Weight Image"),
        anno::in_group("Subsurface")
    ]],
            
    uniform ::OmniSurface::OmniShared::scattering_colors_presets subsurface_scattering_colors_preset = 
        ::OmniSurface::OmniShared::scattering_colors_custom [[
        anno::display_name("Scattering Presets"),
        anno::in_group("Subsurface")
    ]],
    
    color subsurface_transmission_color = color(1.0) [[
        anno::display_name("Color"),
        anno::in_group("Subsurface")
    ]],

    uniform texture_2d subsurface_transmission_color_image = texture_2d() [[ 
        anno::display_name("Color Image"),
        anno::in_group("Subsurface")
    ]],

    color subsurface_scattering_color = color(1.0f, 0.3f, 0.2f) [[
        anno::display_name("Radius (mfp)"),
        anno::description("Multiply with Radius image to get final radius"),
        anno::in_group("Subsurface")
    ]],
            
    uniform texture_2d subsurface_scattering_color_image = texture_2d() [[
        anno::display_name("Radius Image"),
        anno::in_group("Subsurface")
    ]],

    uniform float subsurface_scale = float(0.25f) [[
        anno::display_name("Scale"),
        anno::in_group("Subsurface"),
        anno::soft_range(0.0f, 5.0f)
   ]],

    //Subsurface Mask 
    //
    uniform float Nose_Scatter_Scale = float(1.0) [[
        anno::display_name("Nose_Scatter_Scale"),
        anno::in_group("Subsurface Advanced"),
        anno::hard_range(0.0f, 2.0f)
    ]],
    uniform float Mouth_Scatter_Scale = float(1.0) [[
        anno::display_name("Mouth_Scatter_Scale"),
        anno::in_group("Subsurface Advanced"),
        anno::hard_range(0.0f, 2.0f)
    ]],
    uniform float UpperLid_Scatter_Scale = float(1.0) [[
        anno::display_name("UpperLid_Scatter_Scale"),
        anno::in_group("Subsurface Advanced"),
        anno::hard_range(0.0f, 2.0f)
    ]],
    uniform float InnerLid_Scatter_Scale = float(1.0) [[
        anno::display_name("InnerLid_Scatter_Scale"),
        anno::in_group("Subsurface Advanced"),
        anno::hard_range(0.0f, 2.0f)
    ]],
    uniform float Ear_Scatter_Scale = float(1.0) [[
        anno::display_name("Ear_Scatter_Scale"),
        anno::in_group("Subsurface Advanced"),
        anno::hard_range(0.0f, 2.0f)
    ]],
    uniform float Neck_Scatter_Scale = float(1.0) [[
        anno::display_name("Neck_Scatter_Scale"),
        anno::in_group("Subsurface Advanced"),
        anno::hard_range(0.0f, 2.0f)
    ]],
    uniform float Cheek_Scatter_Scale = float(1.0) [[
        anno::display_name("Cheek_Scatter_Scale"),
        anno::in_group("Subsurface Advanced"),
        anno::hard_range(0.0f, 2.0f)
    ]],
    uniform float Forehead_Scatter_Scale = float(1.0) [[
        anno::display_name("Forehead_Scatter_Scale"),
        anno::in_group("Subsurface Advanced"),
        anno::hard_range(0.0f, 2.0f)
    ]],
    uniform float UpperLip_Scatter_Scale = float(1.0) [[
        anno::display_name("UpperLip_Scatter_Scale"),
        anno::in_group("Subsurface Advanced"),
        anno::hard_range(0.0f, 2.0f)
    ]],
    uniform float Chin_Scatter_Scale = float(1.0) [[
        anno::display_name("Chin_Scatter_Scale"),
        anno::in_group("Subsurface Advanced"),
        anno::hard_range(0.0f, 2.0f)
    ]],
    uniform float Unmasked_Scatter_Scale = float(1.0) [[
        anno::display_name("Unmasked_Scatter_Scale"),
        anno::in_group("Subsurface Advanced"),
        anno::hard_range(0.0f, 2.0f)
    ]],
	///////////////////////////////////////////////////
    //Emission
    //
    float emission_weight = float(0.0) [[ 
        anno::display_name("Weight"),
        anno::in_group("Emission"),
        anno::soft_range(0.0f, 1.0f)
    ]],

    color emission_color = color(1.0) [[
        anno::display_name("Color (nit)"),
        anno::in_group("Emission")
    ]],

    uniform texture_2d emission_color_image = texture_2d() [[ 
        anno::display_name("Color Image"),
        anno::in_group("Emission")
    ]],

    //Geometry
    //
    uniform bool thin_walled = false [[ 
        anno::display_name("Thin Walled"),
        anno::description("Thin surface with no interior."),
        anno::in_group("Geometry")
    ]],

	///////////////////////////////////////////////
	// UV	
	uniform float2 uv_translate =  float2(0.0f)
	[[
		anno::display_name("UV Translate"),
		anno::description("Controls position of texture."),
		anno::in_group("UV")
	]],
	uniform float uv_rotate =  0.f
	[[
		anno::display_name("UV Rotate"),
		anno::description("Rotates angle of texture in degrees."),
		anno::in_group("UV")
	]],
	uniform float2 uv_scale =  float2(1.0f)
	[[
		anno::display_name("UV Scale"),
		anno::description("Larger number increases size of texture."),
		anno::in_group("UV")
	]],
	
	uniform float2 blend_uv_translate =  float2(0.0f)
	[[
		anno::display_name("Blend map UV Translate"),
		anno::description("Controls the position of the blendmap texture."),
		anno::in_group("UV")
	]],
	uniform float  blend_uv_rotate =  0.f
	[[
		anno::display_name("Blend map UV Rotate"),
		anno::description("Rotates angle of the blendmap texture in degrees."),
		anno::in_group("UV")
	]],
	uniform float2 blend_uv_scale =  float2(1.0f)
	[[
		anno::display_name("Blend map UV Scale"),
		anno::description("Larger numbers increase the size of the blendmap texture"),
		anno::in_group("UV")
	]]

)[[
	anno::display_name("OmniRL DHHead"),
    anno::author("Reallusion"),
    anno::version(3, 4, 1, ""),
    anno::modified(2020, 3, 24, "")
    
]] = let {
    
    float2 UV0 = TransformUV( float2(state::texture_coordinate(0).x, state::texture_coordinate(0).y), GetUvFloat3x3( uv_scale, uv_translate, uv_rotate ) );
	float2 uv_blendmap = TransformUV( float2(state::texture_coordinate(0).x, state::texture_coordinate(0).y), GetUvFloat3x3( blend_uv_scale, blend_uv_translate, blend_uv_rotate ) );

	///////////////////////////////////////////////////
    //Base Color
	bool bHasBaseColorMap = tex::texture_isvalid(diffuse_reflection_color_image);
    float3 base_color_ = bHasBaseColorMap ? 
        tex::lookup_float3(diffuse_reflection_color_image,UV0,tex::wrap_repeat,tex::wrap_repeat) :
        float3(diffuse_reflection_weight);

	bool has_blendmap = tex::texture_isvalid(blend_map);
	float3 color_blended = tex::texture_isvalid(blend_map) ? 
        BlendMapByMode( tex::lookup_float3(blend_map,uv_blendmap,tex::wrap_repeat,tex::wrap_repeat), base_color_, blend_map_strength, blend_map_mode ) 
        : base_color_;
	
	bool bHasBlendMap2 = tex::texture_isvalid(BaseColor_Blend2_Image);
	
    float3 vBlendMap2 = bHasBlendMap2 ?
		tex::lookup_float3(BaseColor_Blend2_Image,UV0,tex::wrap_repeat,tex::wrap_repeat)
			: float3(1,1,1);
	float3 vBlend2 = math::lerp( float3(0.5,0.5,0.5), vBlendMap2, BaseColor_Blend2_Strength);
//
	float3 final_blend = ( bHasBaseColorMap || has_blendmap ) ? 
	( bHasBlendMap2 ? IcBlendOverlay( color_blended, vBlend2 ) : color_blended ) 
	: ( math::lerp( float3(1,1,1), vBlendMap2, BaseColor_Blend2_Strength) );
	
	color diffuse_reflection_color_ = diffuse_color_constant * color( final_blend.x, final_blend.y, final_blend.z );
	
	///////////////////////////////////////////////////
	// micro_normal_image

	float2 MicroNormalUv = (UV0 * MicroNormal_Tiling);
	float3 MicroNormalColor = tex::texture_isvalid(micro_normal_image) ? 
	( tex::lookup_float3(micro_normal_image,MicroNormalUv,tex::wrap_repeat,tex::wrap_repeat) * float3(2,2,2) - float3(1,1,1) ) : float3( 0, 0, 1 );

	float MicroNormalMask = tex::texture_isvalid( micro_normal_mask_image ) ? tex::lookup_float(micro_normal_mask_image,UV0,tex::wrap_repeat,tex::wrap_repeat) : 1;
	float MicroNormalStr = MicroNormal_Strength * MicroNormalMask;
	float3 MicroNormal = math::normalize( MicroNormalColor*float3(MicroNormalStr, (Flip_MicroNormal_Y ? -1 : 1 ) * MicroNormalStr,1) );
	
	float micro_normal_unnormalize_x = MicroNormalColor.x * MicroNormalStr; // for micro roughness	
	
    // Normal calculations, RL flip G
    float3 map_normal =  tex::texture_isvalid(geometry_normal_image) ?
                ( tex::lookup_float3(geometry_normal_image,UV0,tex::wrap_repeat,tex::wrap_repeat) * float3(2,2,2) - float3(1,1,1) )
                            : float3( 0, 0, 1 ) ;

// 20210226 NV, geometry_normal_strength = 0 before normal bug is fixed
	float3 map_normal_str = math::normalize( math::lerp( float3(0,0,1), map_normal, geometry_normal_strength ) );
	float3 map_normal_flipY = geometry_normal_image_flip_g_channel ? map_normal_str * float3(1,-1,1) : map_normal_str;

    float3 geometry_normal_0 = map_normal_flipY;

	float3 NormalColor2 = tex::texture_isvalid(NormalMap_Blend_Image) ? 
		( tex::lookup_float3(NormalMap_Blend_Image,UV0,tex::wrap_repeat,tex::wrap_repeat) * float3(2,2,2) - float3(1,1,1) ) : float3( 0, 0, 1 );

	float3 NormalColor2Flip = geometry_normal_image_flip_g_channel ? NormalColor2 * float3(1,-1,1) : NormalColor2;

	float3 Normal2nd = math::normalize( math::lerp( float3( 0, 0, 1 ), NormalColor2Flip, NormalMap_Blend_Strength ) );

	float3 geometry_normal_2 = BlendAngleCorrectedNormals( geometry_normal_0, Normal2nd );
	
	float3 final_normal = BlendAngleCorrectedNormals( geometry_normal_2, MicroNormal );

	float3 geometry_normal_ = math::normalize(
        state::texture_tangent_u(0) * final_normal.x +
        state::texture_tangent_v(0) * final_normal.y +
        state::normal() * (final_normal.z) );
        
	///////////////////////////////////////////////////
	// RL masks
	//
	float2 vNeckEarMask = tex::texture_isvalid( Ear_Neck_Mask_Image ) ?
		tex::lookup_float2(Ear_Neck_Mask_Image,UV0,tex::wrap_repeat,tex::wrap_repeat)
		: float2( 0 );
		
	float4 vNoseMouthUpperInnerLidMask = tex::texture_isvalid( Nose_Mouth_UpperInnerLid_Mask_Image ) ?
		tex::lookup_float4(Nose_Mouth_UpperInnerLid_Mask_Image,UV0,tex::wrap_repeat,tex::wrap_repeat)
		: float4( 0 );
		
	float4 vCheekForeheadUpperLipChinMask = tex::texture_isvalid( Cheek_Fore_UpperLip_Chin_Mask_Image ) ?
		tex::lookup_float4(Cheek_Fore_UpperLip_Chin_Mask_Image,UV0,tex::wrap_repeat,tex::wrap_repeat)
		: float4( 0 );

	///////////////////////////////////////////////////			
	//Roughness
	//
	float roughness_map_value = tex::texture_isvalid( specular_reflection_roughness_image ) ?
		tex::lookup_float(specular_reflection_roughness_image,UV0,tex::wrap_repeat,tex::wrap_repeat)
		: 0.5;
	float fRoughnessLerp = math::lerp( roughness_01, roughness_02, roughness_lerp ); //Jeff 新增
	
    float fRoughnessmultiplier_temp = roughness_multiplier * fRoughnessLerp;//Jeff 新增

	
	float fAreaRoughness =  HQ_HeadRoughnessArea(
		Neck_Roughness_Scale,
		Ear_Roughness_Scale,
		
		Nose_Roughness_Scale,
		Mouth_Roughness_Scale,
		UpperLid_Roughness_Scale,
		InnerLid_Roughness_Scale,
		
		Cheek_Roughness_Scale,
		Forehead_Roughness_Scale,
		UpperLip_Roughness_Scale,
		Chin_Roughness_Scale,
	
		vNeckEarMask,
		vNoseMouthUpperInnerLidMask,
		vCheekForeheadUpperLipChinMask,
		
		Unmasked_Roughness_Scale );

	float fContrastMicroRoughness = CheapContrast( Micro_Roughness_Scale + micro_normal_unnormalize_x, 1 );
	float fLerpContrastMicroRough = math::lerp( 0.85f, 1, fContrastMicroRoughness );
	
	float fMicroRoughness = fAreaRoughness + Micro_Roughness_Scale + micro_normal_unnormalize_x;
	float fEdgeRoughness = roughness_map_value + fMicroRoughness; // + Fresnel * edge_roughness_multiplier

	float roughness_before_sss_weight = math::saturate( fEdgeRoughness * fLerpContrastMicroRough );

// Inner_Mouth_Roughness_Scale = 0.8
//float fMouthCavityMask = vMouthCavityMaskAo.x; // mouth cavity mask, Body is white
	//float specular_reflection_roughness_ = math::saturate( math::lerp( 0.8, face_roughness, fMouthCavityMask ) );
			
	///////////////////////////////////////////////////	
    //Metalness
    //
    
    float metalness_ = tex::texture_isvalid( metalness_image ) ?
		tex::lookup_float(metalness_image,UV0,tex::wrap_repeat,tex::wrap_repeat)
		: 0;
    
    //Specular reflection
    //
    float specular_reflection_weight_ = tex::texture_isvalid( specular_reflection_weight_image ) ?
		tex::lookup_float(specular_reflection_weight_image,UV0,tex::wrap_repeat,tex::wrap_repeat) * specular_reflection_weight
		: specular_reflection_weight;


	///////////////////////////////////////////////////
	//Specular transmission
	float specular_transmission_weight_ = tex::texture_isvalid(specular_transmission_weight_image) ?
		tex::lookup_float(specular_transmission_weight_image, UV0, tex::wrap_repeat,tex::wrap_repeat) * specular_transmission_weight
			: specular_transmission_weight;

	color specular_transmission_color_ =  tex::texture_isvalid(specular_transmission_color_image) ?
        tex::lookup_float3(specular_transmission_color_image, UV0, tex::wrap_repeat,tex::wrap_repeat) * specular_transmission_color
             : specular_transmission_color;
	
	float specular_transmission_scattering_depth_ = tex::texture_isvalid(specular_transmission_scattering_depth_image) ?
		tex::lookup_float(specular_transmission_scattering_depth_image, UV0, tex::wrap_repeat,tex::wrap_repeat) * specular_transmission_scattering_depth
				: specular_transmission_scattering_depth;

	color specular_transmission_scattering_color_ = tex::texture_isvalid(specular_transmission_scattering_color_image) ?
		tex::lookup_float3(specular_transmission_scattering_color_image, UV0, tex::wrap_repeat,tex::wrap_repeat) * specular_transmission_scattering_color
				: specular_transmission_scattering_color;

    float specular_transmission_scatter_anisotropy_= tex::texture_isvalid(specular_transmission_scatter_anisotropy_image) ?
		tex::lookup_float(specular_transmission_scatter_anisotropy_image, UV0, tex::wrap_repeat,tex::wrap_repeat) * specular_transmission_scatter_anisotropy
					: specular_transmission_scatter_anisotropy;
		
    ///////////////////////////////////////////////////
    //Subsurface
    //
    float sss_weight_map = tex::texture_isvalid( subsurface_weight_image )?
    	tex::lookup_float(subsurface_weight_image,UV0,tex::wrap_repeat,tex::wrap_repeat)* subsurface_weight: subsurface_weight;
	float sss_weight =  HQ_HeadSSSArea(
												Neck_Scatter_Scale,
												Ear_Scatter_Scale,
												
												Nose_Scatter_Scale,
												Mouth_Scatter_Scale,
												UpperLid_Scatter_Scale,
												InnerLid_Scatter_Scale,
												
												Cheek_Scatter_Scale,
												Forehead_Scatter_Scale,
												UpperLip_Scatter_Scale,
												Chin_Scatter_Scale,
											
												vNeckEarMask,
												vNoseMouthUpperInnerLidMask,
												vCheekForeheadUpperLipChinMask,
												
												Unmasked_Scatter_Scale, 
												sss_weight_map );
	
	color subsurface_scattering_color_ = tex::texture_isvalid( subsurface_scattering_color_image ) ?
			tex::lookup_float3(subsurface_scattering_color_image,UV0,tex::wrap_repeat,tex::wrap_repeat)
			: float3( subsurface_scattering_color );
 
    color subsurface_transmission_color_ = tex::texture_isvalid(subsurface_transmission_color_image) ?
		tex::lookup_float3(subsurface_transmission_color_image, UV0, tex::wrap_repeat,tex::wrap_repeat) * subsurface_transmission_color
				: subsurface_transmission_color;    

    //Roughness affected by sss_weight
    float sss_roughness = math::lerp(1, fRoughnessmultiplier_temp,  math::saturate(sss_weight*10) );
	float final_roughness = math::saturate( sss_roughness * roughness_before_sss_weight );//Jeff修改
	
    // //Emission
    color emission_color_ = tex::texture_isvalid(emission_color_image) ?
    	color( tex::lookup_float3(subsurface_scattering_color_image,UV0,tex::wrap_repeat,tex::wrap_repeat) )
		: emission_color;

} in
    ::OmniSurface::OmniSurfaceBase::OmniSurfaceBase(

    //Base
    //
    diffuse_reflection_weight: diffuse_reflection_weight,
    diffuse_reflection_color: diffuse_reflection_color_,
	diffuse_reflection_roughness: 1.f,
    //Metalness
    //
    metalness: metalness_,

    //Specular reflection
    //
    specular_reflection_weight: specular_reflection_weight_, //Jeff修改
    specular_reflection_roughness: final_roughness,
	specular_reflection_color : specular_reflection_color,

    specular_reflection_ior_preset: specular_reflection_ior_preset,
    specular_reflection_ior: specular_reflection_ior,
    
	//Specular transmission
	enable_specular_transmission: enable_specular_transmission,
	specular_transmission_weight: specular_transmission_weight_,
	specular_transmission_color: specular_transmission_color_,
	specular_transmission_scattering_depth: specular_transmission_scattering_depth_,
	specular_transmission_scattering_color: specular_transmission_scattering_color_,
    specular_transmission_scatter_anisotropy: specular_transmission_scatter_anisotropy_,
	specular_transmission_dispersion_abbe: specular_transmission_dispersion_abbe,

    //Subsurface
    enable_diffuse_transmission: enable_diffuse_transmission,
    subsurface_weight: sss_weight,
    subsurface_scattering_colors_preset: subsurface_scattering_colors_preset,
    subsurface_transmission_color: subsurface_transmission_color_, //diffuse_reflection_color_, // 20210226 NV subsurface_transmission_color_,
    subsurface_scattering_color: subsurface_scattering_color_,
    subsurface_scale: subsurface_scale,

    //Emission
    //
    emission_weight: emission_weight,
    emission_color: emission_color_,
    //Geometry
    //
    thin_walled: thin_walled,
    geometry_normal: geometry_normal_

    );